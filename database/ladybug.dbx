// -------------------------------------------------------------- //
model buyer (
	key    pk
	unique id

	field pk         serial64
	field created_at timestamp ( autoinsert )
	field updated_at timestamp ( autoinsert, autoupdate )
	field id         text
	field first_name text ( updatable )
	field last_name  text ( updatable )
)

create buyer()

create buyer( noreturn )

read one ( 
    select buyer 
    where buyer.pk = ? 
)

read one (
    select buyer.pk
    where buyer.id = ?
)

read scalar (
    select buyer
    where buyer.pk = ?
)

update buyer ( where buyer.pk = ? )

update buyer ( 
    where buyer.pk = ? 
    noreturn
)

// -------------------------------------------------------------- //
model buyer_email (
	key    pk
	unique id
    unique address

	field pk          serial64
    field buyer_pk    int64
	field created_at  timestamp ( autoinsert )
    field address     text ( updatable )
    field salted_hash text ( updatable )
	field id          text
)

create buyer_email()

create buyer_email ( noreturn )

read all (
    select buyer_email
    where buyer_email.buyer_pk = ?
)

read one (
    select buyer_email
    where buyer_email.address = ?
)

read scalar (
    select buyer_email
    where buyer_email.address = ?
)

update buyer_email ( where buyer_email.address = ? )

update buyer_email ( 
    where buyer_email.address = ? 
    noreturn
)

// -------------------------------------------------------------- //
model address (
	key    pk
	unique id

	field pk             serial64
    field buyer_pk        int64
	field created_at     timestamp ( autoinsert )
    field street_address text ( updatable )
    field city           text ( updatable )
    field state          text ( updatable )
    field zip            int ( updatable )
    field is_billing     bool
	field id             text
)

create address()

create address ( noreturn )

read all (
    select address
    where address.buyer_pk = ?
)

read all (
    select address
    where address.is_billing = true
    where address.buyer_pk = ?
)

read all (
    select address
    where address.is_billing = false
    where address.buyer_pk = ?
)

update address ( where address.pk = ? )



// -------------------------------------------------------------- //
model buyer_session (
	key    pk
	unique id

    field pk          serial64
    field buyer_pk     int64
    field id          text
	field created_at  timestamp ( autoinsert )
)

create buyer_session()

create buyer_session( noreturn )

read one (
    select buyer_session.buyer_pk
    where buyer_session.id = ?
)

read one (
    select buyer_session 
    where buyer_session.buyer_pk = ?
)

read first (
    select buyer_session
    where buyer_session.buyer_pk = ?
)

// -------------------------------------------------------------- //
model vendor (
    key pk
    unique id

    field pk                      serial64
    field id                      text
    field created_at              timestamp ( autoinsert )
    field fein                    text ( updatable )
)

create vendor()

create vendor ( noreturn )

read one (
    select vendor.pk
    where vendor.id = ?
)

// -------------------------------------------------------------- //
//NOTE: this model represents a point of contact for our marketplace not for buyers

model executive_contact ( 
    key pk
    unique id

    field pk                      serial64
    field id                      text
    field vendor_pk               int64
    field first_name              text
    field last_name               text
    field created_at              timestamp ( autoinsert )
)

create executive_contact()

create executive_contact( noreturn )

// -------------------------------------------------------------- //
model vendor_email (
	key    pk
	unique id
    unique address

	field pk                     serial64
	field id                     text
    field executive_contact_pk   int64
	field created_at             timestamp ( autoinsert )
    field address                text ( updatable )
    field salted_hash            text ( updatable )
)

create vendor_email()

create vendor_email( noreturn )

// -------------------------------------------------------------- //
model vendor_phone (
	key    pk
	unique id
    unique phone_number

    field pk                     serial64
    field id                     text
    field executive_contact_pk   int64
    field phone_number           int
    field country_code           int
    field area_code              int
)

create vendor_phone()

create vendor_phone( noreturn )

// -------------------------------------------------------------- //
model vendor_address (
	key    pk
	unique id

	field pk             serial64
    field vendor_pk      int64
	field created_at     timestamp ( autoinsert )
    field street_address text ( updatable )
    field city           text ( updatable )
    field state          text ( updatable )
    field zip            int ( updatable )
    field is_billing     bool
	field id             text
)

create vendor_address()

create vendor_address( noreturn )

// -------------------------------------------------------------- //
model product (
    key pk
    unique id

    field pk               serial64
    field id               text
    field vendor_pk        int64
    field created_at       timestamp ( autoinsert )
    field price            float ( updatable )
    field discount         float ( updatable )
    field discount_active  bool ( updatable )
    field sku              text ( updatable )
    field google_bucket_id text ( updatable )
    field ladybug_approved bool ( updatable )  //this field indicates a review by our marketplace
    field product_active   bool ( updatable )
    field num_in_stock     int  ( updatable )
    field description      text ( updatable )
    field rating           float ( updatable ) //rating reflects the average of all product reviews
)

create product()

create product ( noreturn )

update product ( where product.pk = ? ) 

read one (
    select product.pk product.price
    where product.id = ?
)

read one (
    select product
    where product.id = ?
)

read paged (
   select product
   where product.product_active = true 
   where product.ladybug_approved = true 
   where product.num_in_stock != 0
)

read all (
   select product
   where product.product_active = true 
)

read all (
    select product
    where product.product_active = false
    where product.ladybug_approved = true
)

read count (
    select product
    where product.product_active = false
)

// -------------------------------------------------------------- //
model product_review (
    key pk
    unique id
    
    field pk          serial64
    field id          text
    field buyer_pk    int64
    field product_pk  int64
    field rating      int ( updatable )
    field description text ( updatable )
)

create product_review ()

create product_review ( noreturn )

read has (
    select product_review
    join product.pk = product_review.product_pk
    where product.id = ?
    where product_review.buyer_pk = ?
)

read scalar (
    select product_review
    join product.pk = product_review.product_pk
    where product.id = ?
    where product_review.buyer_pk = ?
)

read one (
    select product_review
    where product_review.pk = ?
)

update product_review (
    where product_review.pk = ?
    noreturn
)

// -------------------------------------------------------------- //
model trial_product (
    key pk
    unique id

    field pk serial64
    field id             text
    field vendor_pk      int64
    field buyer_pk        int64
    field product_pk     int64
    field created_at     timestamp ( autoinsert )
    field trial_price    float
    field is_returned    bool ( updatable )
)

create trial_product ()

// -------------------------------------------------------------- //
model purchased_product (
    key pk
    unique id

    field pk             serial64
    field id             text
    field vendor_pk      int64
    field buyer_pk        int64
    field product_pk     int64
    field purchase_price float
    field created_at     timestamp ( autoinsert )
)

create purchased_product()

create purchased_product( noreturn )

read has (
    select purchased_product
    where purchased_product.buyer_pk = ?
)


// -------------------------------------------------------------- //
model vendor_session (
	key    pk
	unique id

    field pk          serial64
    field vendor_pk   int64
    field id          text
	field created_at  timestamp ( autoinsert )
)

create vendor_session()

create vendor_session( noreturn )

read one (
    select vendor_session.vendor_pk
    where vendor_session.id = ?
)

// -------------------------------------------------------------- //
model conversation (
    key pk
    unique id

    field pk              serial64
    field vendor_pk       int64
    field buyer_pk        int64
    field buyer_unread    bool  ( updatable )
    field vendor_unread   bool  ( updatable )
    field message_count   int64 ( updatable )
    field id              text
    field created_at      timestamp ( autoinsert )
)

create conversation()

read one (
    select conversation
    where conversation.vendor_pk = ?
    where conversation.buyer_pk = ?
)

read one (
    select conversation
    where conversation.id = ?
)

read scalar (
    select conversation
    where conversation.vendor_pk = ?
    where conversation.buyer_pk = ?
)

read all (
    select conversation
    where conversation.vendor_pk = ?
    where conversation.vendor_unread = true
)

read all (
    select conversation
    where conversation.buyer_pk = ?
    where conversation.buyer_unread = true
)

read all (
    select conversation
    where conversation.buyer_pk = ?
)

read all (
    select conversation
    where conversation.vendor_pk = ?
)

read paged (
    select conversation
    where conversation.buyer_pk = ?
)

read paged (
    select conversation
    where conversation.vendor_pk = ?
)

update conversation (
    where conversation.pk = ?
)

update conversation (
    where conversation.pk = ?    
    noreturn
)

// -------------------------------------------------------------- //
model message (
	key    pk
	unique id

    field pk                  serial64
    field id                  text
    field created_at          timestamp ( autoinsert )
    field buyer_sent          bool
    field description         text
    field conversation_pk     int64
    field conversation_number int64
)

create message()

create message( noreturn )

read one (
    select message
    where message.id = ?
)

read all (
    select message
    where message.conversation_pk = ?
)

read limitoffset (
    select message
    where message.conversation_pk = ?
    orderby desc message.created_at
)
